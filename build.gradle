// Apply plug-ins
plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'checkstyle'
    id 'pmd'
    id 'findbugs'
    id 'jacoco'
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'mysql:mysql-connector-java'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Check style configuration
checkstyle {
    toolVersion = '8.11'
}

checkstyleMain.exclude '**/org/spring/Application.java'

// FindBugs configuration
findbugs {
    toolVersion = '3.0.1'
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// PMD configuration
pmd {
    ruleSets = ["java-basic", "java-braces", "java-unnecessary", "java-unusedcode"]
    toolVersion = '6.0.0'
}
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

test {
    testLogging {
        events 'PASSED', 'STARTED', 'FAILED', 'SKIPPED'
    }
}
